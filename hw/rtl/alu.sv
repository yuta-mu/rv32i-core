`include "alu_ops.svh"

module alu (
    input  logic [31:0] a,
    input  logic [31:0] b,
    input  logic [3:0]  alu_op,
    output logic [31:0] result,
    output logic        zero
);

    wire [4:0] shamt = b[4:0];

    assign zero = (result == 0);

    always_comb begin
        case (alu_op)
            ALU_ADD:  result = a + b;
            ALU_SUB:  result = a - b;
            ALU_SLL:  result = a << shamt;
            ALU_SLT:  result = ($signed(a) < $signed(b)) ? 1 : 0;
            ALU_SLTU: result = (a < b) ? 1 : 0;
            ALU_XOR:  result = a ^ b;
            ALU_SRL:  result = a >> shamt;
            ALU_SRA:  result = $signed(a) >>> shamt;
            ALU_OR:   result = a | b;
            ALU_AND:  result = a & b;
            default:  result = 0;
        endcase
    end

endmodule

